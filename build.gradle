project.ext {
    groupName = 'io.lepo'
    pomConfig = {
        organisation {
            name 'Lepo.IO'
            url 'https://lepo.io/'
        }
        scm {
            url "https://github.com/Lepovirta/${rootProject.name}"
            connection "scm:git:https://github.com/Lepovirta/${rootProject.name}"
            developerConnection "scm:git:https://github.com/Lepovirta/${rootProject.name}"
        }
        developers {
            developer {
                id 'jkpl'
                name 'Jaakko Pallari'
            }
        }
    }
}

allprojects {
    project.version = '0.1-SNAPSHOT'

    plugins.withType(JavaPlugin).whenPluginAdded {
        group project.groupName
        version project.version
        sourceCompatibility = 1.8
        archivesBaseName = "${rootProject.name}-${name}"

        repositories {
            mavenCentral()
        }
    }

    plugins.withType(MavenPublishPlugin).whenPluginAdded {
        publishing {
            publications {
                maven(MavenPublication) {
                    groupId project.groupName
                    artifactId archivesBaseName
                    version project.version
                    from components.java
                    pom.withXml {
                        asNode().children().last() + project.pomConfig
                    }
                }
            }
        }
    }
}

project(":cli") {
    apply plugin: 'java'
    apply plugin: 'application'

    mainClassName = 'io.lepo.lukki.cli.Main'

    dependencies {
        compile project(':core')
        compile project(':http')
        compile project(':html')
        compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    }
}

project(":core") {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    dependencies {
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }
}

project(":http") {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    dependencies {
        compile project(':core')
        compile group: 'org.apache.httpcomponents', name: 'httpasyncclient', version: '4.1.3'
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }
}

project(":html") {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    dependencies {
        compile project(':core')
        compile group: 'org.jsoup', name: 'jsoup', version: '1.10.3'
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }
}
