import org.junit.platform.gradle.plugin.JUnitPlatformPlugin

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath group: 'org.junit.platform', name: 'junit-platform-gradle-plugin', version: '1.0.0'
    }
}

project.ext {
    groupName = 'io.lepo'
    pomConfig = {
        organisation {
            name 'Lepo.IO'
            url 'https://lepo.io/'
        }
        scm {
            url "https://github.com/Lepovirta/${rootProject.name}"
            connection "scm:git:https://github.com/Lepovirta/${rootProject.name}"
            developerConnection "scm:git:https://github.com/Lepovirta/${rootProject.name}"
        }
        licenses {
            license {
                name 'GNU Lesser General Public License'
                url 'http://www.gnu.org/licenses/lgpl-2.1.html'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'jkpl'
                name 'Jaakko Pallari'
            }
        }
    }
}

apply plugin: 'gitversion'

subprojects {
    project.version = project.gitVersion

    plugins.withType(JavaPlugin).whenPluginAdded {
        group project.groupName
        version project.version
        sourceCompatibility = 1.8
        archivesBaseName = "${rootProject.name}-${name}"

        repositories {
            mavenCentral()
            maven { url "https://jitpack.io" }
        }

        dependencies {
            compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
        }
    }

    plugins.withType(MavenPublishPlugin).whenPluginAdded {
        publishing {
            publications {
                maven(MavenPublication) {
                    groupId project.groupName
                    artifactId archivesBaseName
                    version project.version
                    from components.java
                    pom.withXml {
                        asNode().children().last() + project.pomConfig
                    }
                }
            }
        }
    }

    plugins.withType(JUnitPlatformPlugin).whenPluginAdded {
        junitPlatform {
            platformVersion '1.0.0'
        }

        dependencies {
            testCompile "org.junit.jupiter:junit-jupiter-api:5.0.0"
            testRuntime "org.junit.jupiter:junit-jupiter-engine:5.0.0"
            testCompile 'org.assertj:assertj-core:3.8.0'
        }
    }

    plugins.withType(CheckstylePlugin).whenPluginAdded {
        checkstyle {
            configFile = file "${project.rootDir}/checkstyle.xml"
            toolVersion = '8.2'
        }
    }
}

project(":cli") {
    apply plugin: 'java'
    apply plugin: 'application'
    apply plugin: 'checkstyle'

    mainClassName = 'io.lepo.lukki.cli.Main'

    dependencies {
        compile project(':core')
        compile project(':http')
        compile project(':html')
        compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    }
}

project(":core") {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'org.junit.platform.gradle.plugin'
    apply plugin: 'checkstyle'

    dependencies {
        testCompile("org.junit.jupiter:junit-jupiter-api:5.0.0")
        testRuntime("org.junit.jupiter:junit-jupiter-engine:5.0.0")
    }
}

project(":http") {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'org.junit.platform.gradle.plugin'
    apply plugin: 'checkstyle'

    dependencies {
        compile project(':core')
        compile group: 'org.apache.httpcomponents', name: 'httpasyncclient', version: '4.1.3'
    }
}

project(":html") {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'org.junit.platform.gradle.plugin'
    apply plugin: 'checkstyle'

    dependencies {
        compile project(':core')
        compile group: 'org.jsoup', name: 'jsoup', version: '1.10.3'
    }
}
