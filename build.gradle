import org.junit.platform.gradle.plugin.JUnitPlatformPlugin
import org.jetbrains.kotlin.gradle.plugin.KotlinPlugin
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    ext {
        kotlinVersion = '1.1.51'
        junitPlatformVersion = '1.0.0'
        jupiterVersion = '5.0.0'
        slf4jVersion = '1.7.25'
        logbackVersion = '1.2.3'
        jsoupVersion = '1.10.3'
        assertjVersion = '3.8.0'
        httpasyncclientVersion = '4.1.3'
        javalinVersion = '0.5.2'
        joddHttpVersion = '3.9.1'
    }

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath group: 'org.junit.platform', name: 'junit-platform-gradle-plugin', version: junitPlatformVersion
        classpath group: 'org.jetbrains.kotlin', name: 'kotlin-gradle-plugin', version: kotlinVersion
    }
}

project.ext {
    groupName = 'io.lepo'
    pomConfig = {
        organisation {
            name 'Lepo.IO'
            url 'https://lepo.io/'
        }
        scm {
            url "https://github.com/Lepovirta/${rootProject.name}"
            connection "scm:git:https://github.com/Lepovirta/${rootProject.name}"
            developerConnection "scm:git:https://github.com/Lepovirta/${rootProject.name}"
        }
        licenses {
            license {
                name 'GNU Lesser General Public License'
                url 'http://www.gnu.org/licenses/lgpl-2.1.html'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'jkpl'
                name 'Jaakko Pallari'
            }
        }
    }
}

apply plugin: 'gitversion'

allprojects {
    repositories {
        mavenCentral()
        jcenter()
    }
}

subprojects {
    project.version = project.gitVersion

    plugins.withType(JavaPlugin).whenPluginAdded {
        group project.groupName
        version project.version
        sourceCompatibility = 1.8
        archivesBaseName = "${rootProject.name}-${name}"

        repositories {
            maven { url 'https://jitpack.io' }
        }

        sourceSets {
            test.resources.srcDirs += "${rootProject.rootDir}/testResources"
        }

        dependencies {
            compile group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
            testCompile group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
            testCompile group: 'org.slf4j', name: 'log4j-over-slf4j', version: slf4jVersion
            testCompile group: 'org.assertj', name: 'assertj-core', version: assertjVersion
        }
    }

    plugins.withType(MavenPublishPlugin).whenPluginAdded {
        publishing {
            publications {
                maven(MavenPublication) {
                    groupId project.groupName
                    artifactId archivesBaseName
                    version project.version
                    from components.java
                    pom.withXml {
                        asNode().children().last() + project.pomConfig
                    }
                }
            }
        }
    }

    plugins.withType(JUnitPlatformPlugin).whenPluginAdded {
        junitPlatform {
            platformVersion junitPlatformVersion
            enableStandardTestTask = true
        }

        dependencies {
            testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: jupiterVersion
            testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: jupiterVersion
        }
    }

    plugins.withType(JacocoPlugin).whenPluginAdded {
        jacocoTestReport {
            reports {
                xml.enabled false
                html.enabled false
                csv.enabled false
            }
        }

        def junitPlatformTestTask = project.tasks.getByName('junitPlatformTest')

        jacoco {
            applyTo junitPlatformTestTask
        }

        project.task(type: JacocoReport, "junitPlatformJacocoReport",
                {
                    sourceDirectories = files("./src/main")
                    classDirectories = files("$buildDir/classes/main")
                    executionData junitPlatformTestTask
                }
        )
    }

    plugins.withType(KotlinPlugin).whenPluginAdded {
        dependencies {
            compile "org.jetbrains.kotlin:kotlin-stdlib-jre8"
        }

        sourceSets {
            main.java.srcDirs += 'src/main/kotlin'
            test.java.srcDirs += 'src/test/kotlin'
        }
    }

    tasks.withType(KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}

project(':core') {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'org.junit.platform.gradle.plugin'
    apply plugin: 'jacoco'
}

project(':examples') {
    apply plugin: 'java'
    apply plugin: 'application'

    mainClassName = 'io.lepo.lukki.examples.Main'

    dependencies {
        compile project(':core')
        compile project(':http')
        compile project(':html')
        compile group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
        compile group: 'org.slf4j', name: 'log4j-over-slf4j', version: slf4jVersion
    }
}

project(':integration') {
    apply plugin: 'java'
    apply plugin: 'org.junit.platform.gradle.plugin'
    apply plugin: 'jacoco'

    dependencies {
        testCompile project(':core')
        testCompile project(':html')
        testCompile project(':http')
        testCompile project(':testserver')
    }
}

project(':html') {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'org.junit.platform.gradle.plugin'
    apply plugin: 'jacoco'

    dependencies {
        compile project(':core')
        compile group: 'org.jsoup', name: 'jsoup', version: jsoupVersion
    }
}

project(':http') {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'org.junit.platform.gradle.plugin'
    apply plugin: 'jacoco'

    dependencies {
        compile project(':core')
        compile group: 'org.apache.httpcomponents', name: 'httpasyncclient', version: httpasyncclientVersion
    }
}

project(':testserver') {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'org.junit.platform.gradle.plugin'
    apply plugin: 'jacoco'
    apply plugin: 'kotlin'

    dependencies {
        compile group: 'io.javalin', name: 'javalin', version: javalinVersion
        testCompile group: 'org.jsoup', name: 'jsoup', version: jsoupVersion
        testCompile group: 'org.jodd', name: 'jodd-http', version: joddHttpVersion
    }
}

apply plugin: 'jacoco'

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include('**/build/jacoco/*.exec')

    subprojects.each {
        sourceSets(it.sourceSets.main)
    }

    onlyIf = {
        true
    }

    reports {
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/report.xml"
        html.enabled false
        csv.enabled false
    }
}
